managed implementation in class zbp_np_travel_i unique;
strict ( 2 ); // following strict syntax

with draft;

define behavior for ZNP_TRAVEL_I alias Travel
persistent table znp_travel
draft table znp_d_travel
lock master
total etag LastChangedAt
authorization master ( global, instance )
etag master LocalLastChangedAt // system detect the latest timestamp and prevent overwrite
{
  create ( authorization : global ); // user can create new travel
  update;                            // can change
  delete;                            // can delete/cancel (including it's child 'booking/supplement')
  field(numbering : managed,readonly) TravelUuid; // non editable field in fiori screen
  field(readonly) travelid, OverallStatus;
  association _Booking { create; with draft;} // users can do the bookings to the current travel

  draft action Edit;
  draft action Discard;
  draft action Resume;
  draft action Activate optimized;
  draft determine action Prepare;


  // determination
  determination setTravelId on save { create; update;}
  determination setOverallStatus on modify { create; update;}

  // Do mapping between cds entity alias names and database level fields
  mapping for znp_travel
  {
  // cds entity alias names (LHS) = database level field name (RHS)
  TravelUuid         = travel_uuid;
  TravelId           = travel_id;
  AgencyId           = agency_id;
  CustomerId         = customer_id;
  BeginDate          = begin_date;
  EndDate            = end_date;
  BookingFee         = booking_fee;
  TotalPrice         = total_price;
  CurrencyCode       = currency_code;
  Description        = description;
  OverallStatus      = overall_status;
  LocalCreatedBy     = local_created_by;
  LocalCreatedAt     = local_created_at;
  LocalLastChangedBy = local_last_changed_by;
  LocalLastChangedAt = local_last_changed_at;
  LastChangedAt      = last_changed_at;
  }
}

define behavior for ZNP_BOOKINGS_I alias Booking
persistent table znp_bookings
draft table znp_d_bookings
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
{
  update; // user can update once enters in the travel
  delete; // delete/cancel the booking and its supplement
  field (readonly) TravelUuid, Bookingid, Bookingdate;
  field (numbering : managed, readonly ) BookingUuid; //this one also generated by framework and not editable
  association _Travel{ with draft;}
  association _supplement { create; with draft; } //users can do the supplement to the current Booking uder the travel

  determination setBookingid on save { create; }
  determination setBookingdate on save { create;}

  mapping for znp_bookings
  {
  BookingUuid        = booking_uuid;
  TravelUuid         = parent_uuid;
  BookingId          = booking_id;
  BookingDate        = booking_date;
  CustomerId         = customer_id;
  CarrierId          = carrier_id;
  ConnectionId       = connection_id;
  FlightDate         = flight_date;
  FlightPrice        = flight_price;
  CurrencyCode       = currency_code;
  BookingStatus      = booking_status;
  LocalLastChangedAt = local_last_changed_at;
  }

}

define behavior for ZNP_Supplement_I alias Supplement
persistent table znp_supplement
draft table znp_d_supplement
lock dependent by _travel
authorization dependent by _travel
etag master LocalLastChangedAt
{
  update;
  delete;
  field ( numbering : managed, readonly ) SupplementUuid;
  field (readonly) BookingUuid, TravelUuid, BookingSupplementId;
  association _travel{ with draft;}
  association _booking{ with draft;}

  determination setBookingSupplId on save { create; }

  mapping for znp_supplement
  {
    SupplementUuid      = supplement_uuid;
    TravelUuid          = root_uuid;
    BookingUuid         = parent_uuid;
    BookingSupplementId = booking_supplement_id;
    SupplementId        = supplement_id;
    Price               = price;
    CurrencyCode        = currency_code;
    LocalLastChangedAt  = local_last_changed_at;
  }

}